plugins {
    id 'maven-publish'
    id 'net.fabricmc.fabric-loom-remap' version '1.15-SNAPSHOT'
    id 'ploceus' version '1.15-SNAPSHOT'
}

base {
    archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/mcbig.classtweaker")

    mods {
        "mcbig" {
            sourceSet sourceSets.main
        }
    }
}

ploceus {
    setIntermediaryGeneration(2)
    disableLvtPatch() // Lvt patching is still experimental right now and leads to decompiler issues
}

repositories {
    maven {
        url "https://libraries.minecraft.net"
    }
    maven { url 'https://mvn.devos.one/releases' }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings ploceus.layeredMappings {
        mappings("me.alphamode:nostalgia:${project.nostalgia_mappings}:v2", {
            containsUnpick()
        })
    }

    implementation(files("libs/nostalgia-b1.7.3+build.51-constants.jar"))

    implementation(include('com.mojang:brigadier:1.3.10'))

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    clientExceptions ploceus.raven(project.client_raven_build, 'client')
    serverExceptions ploceus.raven(project.server_raven_build, 'server')

    clientSignatures ploceus.sparrow(project.client_sparrow_build, 'client')
    serverSignatures ploceus.sparrow(project.server_sparrow_build, 'server')

    clientNests ploceus.nests(project.client_nests_build, 'client')
    serverNests ploceus.nests(project.server_nests_build, 'server')

//    testImplementation("org.junit.jupiter:junit-jupiter:6.0.0")
    testImplementation platform('org.junit:junit-bom:6.0.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 25
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25

    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {

    }
}
